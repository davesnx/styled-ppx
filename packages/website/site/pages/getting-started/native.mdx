---
title: Getting Started - Native
showAllLanguage: false
---

import { Callout } from "nextra/components";
import Link from "next/link";

# Native

This guide assumes that you'll use [opam](https://github.com/ocaml/opam) and [dune](https://github.com/ocaml/dune) and have a server or native process running.

### Install

```bash
opam install styled-ppx
```

### Packages available

- `styled-ppx` is the ppx
- `styled-ppx.lib_native` is the library with the CSS bindings, and the implementation of `emotion.sh` on the server. Capable of storing CSS, hashing it and generating a unique classnames, autoprefixing, etc.

### Usage

Add `styled-ppx` under dune's preprocess pps for any library or executable. Add `styled-ppx.lib-native` as libraries.

```diff
(library
  (name ...)
  (libraries
+  styled-ppx.lib-native
   server-reason-react)
  (preprocess
   (pps
+   styled-ppx
    melange.ppx
    server-reason-react-ppx)))
```

```diff
(melange.emit
  (target ...)
  (libraries
+  styled-ppx.css_native
+  styled-ppx.emotion_native
   server-reason-react)
  (preprocess
   (pps
+   styled-ppx
    melange.ppx
    server-reason-react-ppx)))
```

Note: `server-reason-react` and `server-reason-react-ppx` are optional, and only needed if you use styled components (`[%styled.div {||}]`).

### API

The summary would be to use `<CssJs.style_tag />` in your `<head>` which generates the `<style>` tag.

### Example

```reasonml
/* This is a server-reason-react module with those styles encoded as a unique className */
module Link = [%styled.a (~color=CssJs.hex("4299E1")) => {|
  font-size: 1.875rem;
  line-height: 1.5;
  text-decoration: none;
  margin: 0px;
  padding: 10px 0px;
  color: $(color);
|}];

/* This is a unique className pointing to those styles */
let layout = [%cx {|
  display: flex;
  width: 100%;
  height: 100%;
  justify-content: center;
  align-items: center
|}];

/* Later in a component */
let app =
  <div className=layout>
    <Link
      color={CssJs.hex("333333")}
      href="https://sancho.dev"
      rel="noopener noreferrer">
      {React.string("sancho.dev")}
    </Link>
  </div>;
```

To generate the CSS on the server, you would need to use `<CssJs.style_tag />`. This component will render a `<style>` with all generated stylesheet.

Given a `Page` component to simulate a real-world scenario:

```reasonml
module Page = {
  [@react.component]
  let make = () => {
    <html>
      <head>
        <CssJs.style_tag />
      </head>
    </html>
  }
}
```

If you're using dynamic CSS values, such as [dynamic-components](./dynamic-components) or [interpolation](./interpolation) based on runtime values. You would need to evaluate your app before extracting all the CSS.

```reasonml
module Component = {
  [@react.component]
  let make = (~value) => {
    let className = switch (value) {
      | Some(value) => [%cx "margin: $(value)"]
      | None => [%cx "margin: 0"]
    };
    <div className />
  }
};
```

This component needs to be rendered before the `CssJs.style_tag` otherwise the `[%cx]` calls won't be added to the stylesheet, since they aren't being executed yet.

To solve this, we recommend: render the React application first, and then inject the result as `dangerouslySetInnerHTML`.

```reasonml
/* `App` here is the entry component of your React application, while the `Document` component is only meant to run on the server. */
module Document = {
  [@react.component]
  let make = () => {
    let app = ReactDOM.renderToString(<App />);

    <html>
      <head>
        <CssJs.style_tag />
      </head>
      <body>
        <div id="#root" dangerouslySetInnerHTML={"__html": app} />
      </body>
    </html>
  }
};

/* Let's asume we are running [dream](https://github.com/aantron/dream) */
let some_server_side_handler = _request => {
  /* Here we render the entire HTML */
  Dream.html(ReactDOM.renderToString(<Document />));
};
```

### Advanced

If you don't want to render the stylesheet directly, you can use `CssJs.get_stylesheet` to obtain the `stylesheet` as a `string`. In this case, the hydration with the client won't be supported.

To make sure hydration works, you would need the following `<style/>` tag

```ocaml
React.createElement "style"
  [
    Bool ("data-s", true);
    String ("data-emotion", "css " ^ CssJs.get_string_style_hashes ());
    DangerouslyInnerHtml (CssJs.get_stylesheet ());
  ]
  []
```
